apply plugin: 'com.android.application'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId rootProject.ext.packgeName
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }
    signingConfigs {
        sign {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.sign
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.sign
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':note')
    implementation project(':amap')
    implementation project(':common')
    implementation "com.android.support:appcompat-v7:$rootProject.ext.supportVersion"
    implementation "com.android.support:design:$rootProject.ext.supportVersion"
    // Easypermission
    implementation "pub.devrel:easypermissions:$rootProject.ext.easyPermission_version"
    // Butterknife
    implementation "com.jakewharton:butterknife:$rootProject.ext.butterknifVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.ext.butterknifVersion"
    // Espresso
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.ext.espressoVersion"
    // Room
    implementation "android.arch.persistence.room:runtime:$rootProject.ext.roomVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$rootProject.ext.roomVersion"
    implementation 'com.android.support:multidex:1.0.3'
    // leakcanary
//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.1'
//    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.1'
//    // Optional, if you use support library fragments:
//    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.1'
    //ARouter
    implementation "com.alibaba:arouter-api:$rootProject.ext.arouter_api"
    annotationProcessor "com.alibaba:arouter-compiler:$rootProject.ext.arouter_compiler"
    // EventBus
    implementation "org.greenrobot:eventbus:$rootProject.ext.eventbus_version"
}
